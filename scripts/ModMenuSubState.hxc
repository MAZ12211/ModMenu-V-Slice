import funkin.graphics.FunkinCamera;
import funkin.graphics.FunkinSprite;
import funkin.graphics.shaders.GaussianBlurShader;
import funkin.input.Cursor;
import funkin.modding.base.ScriptedMusicBeatSubState;
import funkin.modding.PolymodHandler;
import funkin.util.FileUtilSandboxed;
import funkin.util.ReflectUtil;
import funkin.util.SerializerUtil;
import funkin.util.VersionUtil;
import funkin.util.WindowUtil;

import flixel.FlxG;
import flixel.FlxSubState;
import flixel.graphics.frames.FlxImageFrame;
import flixel.text.FlxText;
import flixel.text.FlxTextBorderStyle;

import polymod.ModMetadata;

import openfl.display.BitmapData;
import openfl.filters.ShaderFilter;
import openfl.utils.ByteArrayData;

import haxe.ds.StringMap;
import haxe.ui.RuntimeComponentBuilder;
import haxe.ui.components.Button;
import haxe.ui.components.Image;
import haxe.ui.components.Label;
import haxe.ui.components.Link;
import haxe.ui.components.Spacer;
import haxe.ui.components.Switch;
import haxe.ui.components.TextField;
import haxe.ui.containers.HBox;
import haxe.ui.containers.ListView;
import haxe.ui.containers.ScrollView;
import haxe.ui.containers.VBox;
import haxe.ui.core.ItemRenderer;
import haxe.ui.data.ArrayDataSource;
import haxe.ui.events.MouseEvent;
import haxe.ui.events.UIEvent;
import haxe.ui.layouts.HorizontalLayout;

import Array;
import StringTools;

class ModMenuSubState extends ScriptedMusicBeatSubState {
    var enabledMods:Array<ModMetadata>;
    var disabledMods:Array<String> = [];
    var totalMods:Int;
    var modStates:StringMap<Bool> = new StringMap();
    var cachedMeta:ModMetadata;
    var modMenuCam:FunkinCamera;
    var currentSearchFilter:String = "";
    var lastFilteredCount:Int = -1;

    var modsBox:HBox = new HBox();
    var modsTxt:Label = new Label();
    var disModsTxt:Label = new Label();

    var menuTitle:FlxText = new FlxText();
    var modTitle:Label = new Label();
    var modSearch:TextField = new TextField();
    var modInfo:Label = new Label();
    var iconSprite:FunkinSprite = new FunkinSprite();
    var modsListView:ListView = new ListView();
    var modInfoView:ScrollView = new ScrollView();
    var itemRenderer:ItemRenderer = new ItemRenderer();
    var modInfoContainer:VBox = new VBox();

    var buttons:Array<Button> = [];
    var homepageBtn:Button;
    var issuesBtn:Button;
    var modsDirBtn:Button;
    var backButton:Button;
    var modDirBtn:Button;
    var configBtn:Button;

    var folderIcon:FunkinSprite = FunkinSprite.create(0, 0, "modmenu/ui/folder-icon");
    var configIcon:FunkinSprite = FunkinSprite.create(0, 0, "modmenu/ui/config-icon");

    var prevPersUpd:Bool;
    var prevCursor:Bool;

    override function create() {
        super.create();

        prevCursor = FlxG.mouse.visible;
        prevPersUpd = FlxG.state.persistentUpdate;
        FlxG.state.persistentUpdate = false;

        enabledMods = PolymodHandler.getAllMods();
        disabledMods = FileUtilSandboxed.readDir("mods").filter(id -> !PolymodHandler.loadedModIds.contains(id));
        totalMods = enabledMods.length + disabledMods.length;

        folderIcon.setGraphicSize(35, 35);
        configIcon.setGraphicSize(35, 35);

        setupCamera();
        setupUI();
        setupButtons();
        setupEventHandlers();

        Cursor.show();
    }

    override function update(elapsed:Float) {
        super.update(elapsed);

        //if (controls.BACK) { close(); cleanup(); }
    }

    // Helper methods
    function setupCamera() {
        FlxG.state.camera.filters = [new ShaderFilter(new GaussianBlurShader(1))];
        modMenuCam = new FunkinCamera("modMenuCam");
        modMenuCam.bgColor = 0x5F000000;
        FlxG.cameras.add(modMenuCam, false);
    }

    function setupUI() {
        var handlerWindow = RuntimeComponentBuilder.fromAsset(Paths.ui("base-style"));
        if (handlerWindow == null) return;
        var mainMenuUI = RuntimeComponentBuilder.fromAsset(Paths.ui("mod-menu/style"));
        if (mainMenuUI == null) return;

        // Setup list view
        modsListView.id = "modListView";
        modsListView.styleNames = "modMenuView";
        modsListView.camera = modMenuCam;
        modsListView.setPosition(50, 125);
        modsListView.setSize(575, 525);

        itemRenderer.id = "modItemRenderer";
        itemRenderer.layout = new HorizontalLayout();

        itemRenderer.addComponent(createComponent("Image", { id: "icon" }));
        var textContainer = new VBox();
        textContainer.percentWidth = 100;
        textContainer.addComponent(createComponent("Label", { id: "text" }));
        var summary:Label = createComponent("Label", {id: "summary" });
        textContainer.addComponent(summary);
        itemRenderer.addComponent(textContainer);
        itemRenderer.addComponent(createComponent("Switch", { id: "enableSwitch", styleNames: "pill-switch" }));

        modsListView.itemRenderer = itemRenderer;
        populateModList(modsListView, "");
        add(modsListView);

        // Setup info view
        modInfoView.id = "modInfoView";
        modInfoView.styleNames = "modMenuView";
        modInfoView.camera = modMenuCam;
        modInfoView.setPosition(650, 225);
        modInfoView.setSize(575, 425);
        add(modInfoView);

        // Setup title
        menuTitle.setFormat(Paths.font("Tardling-Solid.ttf"), 30, 0xFFFFFF, "center", FlxTextBorderStyle.OUTLINE_FAST, 0xFF111111);
        menuTitle.borderSize = 0.75;
        menuTitle.text = "Mods";
        menuTitle.x = modsListView.x + modsListView.width * 0.5 - menuTitle.width * 0.5;
        menuTitle.y = 16;
        menuTitle.camera = modMenuCam;
        add(menuTitle);

        // Setup search field
        modSearch.id = "modSearch";
        modSearch.placeholder = "Search...";
        modSearch.width = modsListView.width * 0.8;
        modSearch.height = 30;
        modSearch.x = modsListView.x + modsListView.width * 0.5 - modSearch.width * 0.5;
        modSearch.y = menuTitle.y + modSearch.height + 5;
        add(modSearch);

        // Setup size text
        modsTxt.text = "Showing " + totalMods + " mods";
        modsTxt.styleNames = "type-enabled";
        modsBox.x = modsListView.x + 10;
        modsBox.y = modsListView.y - 30;
        modsBox.addComponent(modsTxt);
        if (disabledMods.length > 0) {
            disModsTxt.text = " (+" + disabledMods.length + " disabled)";
            disModsTxt.styleNames = "type-disabled";
            modsBox.addComponent(disModsTxt);
        }
        add(modsBox);

        // Setup icon
        iconSprite.x = modInfoView.x;
        iconSprite.y = modInfoView.y - 165;
        iconSprite.visible = false;
        iconSprite.camera = modMenuCam;
        add(iconSprite);

        // Setup mod title and info
        modTitle.addClass("titleLabel");
        modTitle.x = iconSprite.x + 110;
        modTitle.y = iconSprite.y + 5;
        add(modTitle);

        modInfo.addClass("infoLabel");
        modInfo.x = modTitle.x;
        modInfo.y = modTitle.y + 25;
        add(modInfo);

        // Setup mod info container
        modInfoContainer.styleNames = "modInfoContainer";
        modInfoView.addComponent(modInfoContainer);
    }

    function createComponent(type:String, props:Dynamic):Dynamic {
        var comp = switch (type) {
            case "Image": new Image();
            case "Label": new Label();
            case "Switch": new Switch();
            default: null;
        }
        for (field in ReflectUtil.getFieldsOf(props)) ReflectUtil.setProperty(comp, field, ReflectUtil.getAnonymousField(props, field));
        return comp;
    }

    function setupButtons() {
        // Button configurations
        var buttonConfigs = [
            {name: "modDirBtn", text: "", x: modInfoView.x + modInfoView.width - 48, y: modInfoView.y - 105, width: 48, height: 48, disabled: true,
                onClick: () -> FileUtilSandboxed.openFolder("mods/" + modDirBtn.userData)},
            {name: "configBtn", text: "", x: modInfoView.x + modInfoView.width - 116, y: modInfoView.y - 105, width: 48, height: 48, disabled: false,
                onClick: () -> this.openSubState(ScriptedMusicBeatSubState.init("ModConfigSubState"))},
            {name: "homepageBtn", text: "Homepage", x: iconSprite.x + 10, y: modInfoView.y - 50, width: Std.int(modInfoView.width * 0.5 - 30), height: 35, disabled: true,
                onClick: () -> WindowUtil.openURL(homepageBtn.userData)},
            {name: "issuesBtn", text: "Issues", x: iconSprite.x + Std.int(modInfoView.width * 0.5) + 20, y: modInfoView.y - 50, width: Std.int(modInfoView.width * 0.5 - 30), height: 35, disabled: true,
                onClick: () -> WindowUtil.openURL(issuesBtn.userData)},
            {name: "modsDirBtn", text: "Open Mods Folder", x: modsListView.x + modsListView.width - Std.int(modInfoView.width * 0.5 - 30) + 5, y: modsListView.y + modsListView.height + 20, width: Std.int(modInfoView.width * 0.5 - 30), height: 35,
                onClick: () -> FileUtilSandboxed.openFolder("mods")},
            {name: "backButton", text: "Exit Menu", x: modsListView.x + modsListView.width + 20, y: modsListView.y + modsListView.height + 20, width: Std.int(modInfoView.width * 0.5 - 30), height: 35,
                onClick: () -> close()}
        ];

        // Create buttons from config
        for (config in buttonConfigs) {
            var btn = new Button();
            btn.id = "modButton";
            btn.camera = modMenuCam;
            btn.text = config.text;
            btn.x = config.x;
            btn.y = config.y;
            btn.width = config.width;
            btn.height = config.height;
            if (config.disabled != null) btn.disabled = config.disabled;
            btn.onClick = (event:MouseEvent) -> config.onClick();

            switch (config.name) {
                case "modDirBtn": modDirBtn = btn;
                case "configBtn": configBtn = btn;
                case "homepageBtn": homepageBtn = btn;
                case "issuesBtn": issuesBtn = btn;
                case "modsDirBtn": modsDirBtn = btn;
                case "backButton": backButton = btn;
            }

            buttons.push(btn);
            add(btn);
        }

        modDirBtn.add(folderIcon);
        configBtn.add(configIcon);
    }

    function setupEventHandlers() {
        modsListView.onChange = (event:UIEvent) -> {
            var selectedItem = modsListView.selectedItem;
            if (selectedItem.type == "disabled")
                updateModInfo(modInfoView, FileUtilSandboxed.readJSONFromPath("mods/" + disabledMods[selectedItem.originalIndex] + "/_polymod_meta_disabled.json"), true);
            else
                updateModInfo(modInfoView, enabledMods[selectedItem.originalIndex], false);
        }
        modDirBtn.onMouseOver = (event:MouseEvent) -> folderIcon.setColorTransform(1, 1, 1, 1, 255, 255, 255);
        modDirBtn.onMouseOut = (event:MouseEvent) -> folderIcon.setColorTransform(1, 1, 1, 1, 0, 0, 0);
        configBtn.onMouseOver = (event:MouseEvent) -> configIcon.setColorTransform(1, 1, 1, 1, 255, 255, 255);
        configBtn.onMouseOut = (event:MouseEvent) -> configIcon.setColorTransform(1, 1, 1, 1, 0, 0, 0);
        modSearch.onDblClick = (event:MouseEvent) -> modSearch.text = "";
        modSearch.onChange = (event:UIEvent) -> {
            var searchText = event.target.text;
            var newFilteredCount = getFilteredModCount(searchText);
            if (newFilteredCount != lastFilteredCount || searchText != currentSearchFilter) {
                currentSearchFilter = searchText;
                lastFilteredCount = newFilteredCount;

                if (searchText == "") {
                    modsListView.dataSource.clearFilter();
                } else {
                    applySearchFilter(modsListView, searchText);
                }
            }

            updateModsSizeText(searchText);
        }
    }

    function populateModList(list:ListView, searchFilter:String = "") {
        if (list.dataSource == null) list.dataSource = new ArrayDataSource();
        list.dataSource.clear();

        for (i in 0...enabledMods.length) {
            var mod = enabledMods[i];

            var iconResource = FlxImageFrame.fromImage(getModIcon(mod)).frame;

            list.dataSource.add({
                originalIndex: i,
                type: "enabled",
                modId: mod.id,
                iconResource: iconResource,
                icon: { resource: iconResource, opacity: 1 },
                text: { text: mod.title, styleNames: "type-enabled" },
                summary: { text: mod.metadata.get("summary") ?? "" },
                enableSwitch: { selected: true,
                    onClick: (event:MouseEvent) -> toggleMod(mod, event.target.value, false)
                }
            });
        }

        for (i in 0...disabledMods.length) {
            var modId = disabledMods[i];
            var iconBitmap:BitmapData;
            if (FileUtilSandboxed.fileExists("mods/" + modId + "/_polymod_icon.png"))
                iconBitmap = BitmapData.fromFile("mods/" + modId + "/_polymod_icon.png");
            else {
                var defaultPath = FlxG.random.bool(5) ? "flixel/images/logo/default.png" : Paths.image("modmenu/default_icon");
                iconBitmap = Assets.getBitmapData(defaultPath);
            }
            var iconResource = FlxImageFrame.fromImage(iconBitmap).frame;

            list.dataSource.add({
                originalIndex: i,
                type: "disabled",
                modId: modId,
                iconResource: iconResource,
                icon: { resource: iconResource, opacity: 0.3 },
                text: { text: modId, styleNames: "type-disabled" },
                summary: { text: "" },
                enableSwitch: { selected: false,
                    onClick: (event:MouseEvent) -> toggleMod(disabledMods[i], event.target.value, true)
                }
            });
        }

        if (searchFilter != "") applySearchFilter(list, searchFilter);
    }

    function updateModInfo(infoView:ScrollView, modMeta:ModMetadata, disabled:Bool) {
        if (cachedMeta == modMeta) return;
        cachedMeta = modMeta;

        iconSprite.loadBitmapData(getModIcon(modMeta, disabled));
        iconSprite.setGraphicSize(100, 100);
        iconSprite.updateHitbox();
        iconSprite.visible = true;

        modTitle.text = modMeta.title;

        var version = !disabled ? SerializerUtil.serializeVersion(modMeta.modVersion) : "Disabled";
        modInfo.text = version;
        updateButton(homepageBtn, modMeta.homepage);
        updateButton(issuesBtn, !disabled ? modMeta.metadata.get("issues") : modMeta.metadata?.issues);

        modDirBtn.disabled = false;
        modDirBtn.userData = modMeta.id;

        modInfoContainer.removeAllComponents();

        // Description
        if (modMeta.description != null && modMeta.description != "") {
            addLabel(modMeta.description);
            addSpacer();
        }

        // Description
        if (modMeta.license != null && modMeta.license != "") {
            addLabel("License:");
            addLabel(" " + modMeta.license);
            addSpacer();
        }

        // Extra Links
        if (disabled) {
            var extraLinks = modMeta.metadata?.extraLinks;
            if (modMeta?.metadata?.extraLinks != null) {
                if (extraLinks != null && extraLinks != "") {
                    addLabel("Extra Links:");

                    for (fieldName in ReflectUtil.getFieldsOf(extraLinks)) {
                        var url = ReflectUtil.getProperty(extraLinks, fieldName);
                        if (url != null && url != "") {
                            var link = new Link();
                            link.styleNames = "modInfo";
                            link.text = " " + fieldName;
                            link.onClick = (event:MouseEvent) -> WindowUtil.openURL(url);
                            modInfoContainer.addComponent(link);
                        }
                    }

                    addSpacer();
                }
            }
        } else {
            if (modMeta.metadata.exists("extraLinks")) {
                var extraLinks = modMeta.metadata.get("extraLinks");
                if (extraLinks != null && extraLinks != "") {
                    addLabel("Extra Links:");

                    // Convert string to map
                    var linksMap = new StringMap();
                    var cleanString = extraLinks.substring(1, extraLinks.length - 1); // Remove brackets
                    var pairs = cleanString.split(",");

                    for (pair in pairs) {
                        var parts = StringTools.trim(pair).split(" => ");
                        if (parts.length == 2) linksMap.set(StringTools.trim(parts[0]), StringTools.trim(parts[1]));
                    }

                    // Use the map
                    for (key in linksMap.keys()) {
                        var link = new Link();
                        link.styleNames = "modInfo";
                        link.text = " " + key;
                        link.onClick = (event:MouseEvent) -> WindowUtil.openURL(linksMap.get(key));
                        modInfoContainer.addComponent(link);
                    }

                    addSpacer();
                }
            }
        }

        // Contributors
        if (modMeta.contributors != null && modMeta.contributors.length > 0) {
            addLabel("Credits:");

            for (contributor in modMeta.contributors) {
                var nameText = " " + contributor.name;
                if (contributor.role != null && contributor.role != "") {
                    nameText += " - " + contributor.role;
                }

                if (contributor.url != null && contributor.url != "") {
                    var link = new Link();
                    link.styleNames = "modInfo";
                    link.text = nameText;
                    link.onClick = (event:MouseEvent) -> WindowUtil.openURL(contributor.url);
                    modInfoContainer.addComponent(link);
                } else {
                    addLabel(nameText);
                }
            }
        }

        // Fallback
        if (modInfoContainer.numComponents == 0) addLabel("No description available for this mod.");
    }

    function toggleMod(modMeta:ModMetadata, enable:Bool, disabled:Bool) {
        var modId = disabled ? modMeta : modMeta.id;

        modStates.set(modId, enable);
        var removeState:Bool = !enable == disabled;
        if (removeState) modStates.remove(modId);

        if (disabled) {
            modMeta = FileUtilSandboxed.readJSONFromPath("mods/" + modId + "/_polymod_meta_disabled.json");
            handleDependencies(modMeta, removeState);
        } else {
            if (modMeta.dependencies != null) handleDependents(modId, removeState);
        }
    }

    function handleDependencies(dependencies, removeState:Bool) {
        if (dependencies.dependencies != null) {
            var depFields = ReflectUtil.getFieldsOf(dependencies.dependencies);
            for (depName in depFields) {
                modStates.set(depName, true);
                if (removeState) modStates.remove(depName);
                trace(modStates);
                trace(depName);
            }
        }

    }

    function handleDependents(modId:String, removeState:Bool) {
        for (mod in enabledMods.filter(meta -> meta.dependencies.exists(modId))) {
            modStates.set(mod.id, false);
            if (removeState) modStates.remove(mod.id);
            trace("Found dependents mods: " + mod.id);
            trace(mod.id);
        }

        for (mod in enabledMods.filter(meta -> meta.optionalDependencies.exists(modId))) {
            modStates.set(mod.id, false);
            if (removeState) modStates.remove(mod.id);
            trace("Found optional dependents mods: " + mod.id);
            trace(mod.id);
        }
    }

    function addLabel(text:String):Label {
        var label = new Label();
        label.styleNames = "modInfo";
        label.text = text;
        modInfoContainer.addComponent(label);
        return label;
    }

    function addSpacer() {
        var spacer = new Spacer();
        spacer.height = 15;
        modInfoContainer.addComponent(spacer);
    }

    function updateButton(btn:Button, url:String) {
        btn.disabled = (url == null || url == "");
        if (!btn.disabled) btn.userData = url;
    }

    function updateModsSizeText(searchFilter:String = "") {
        if (searchFilter == "") {
            modsTxt.text = "Showing " + totalMods + " mods";
            if (disabledMods.length > 0) disModsTxt.text = "(+" + disabledMods.length + " disabled)";
        } else {
            // Count filtered disabled mods for display
            var filteredDisabledCount = 0;
            for (modId in disabledMods) {
                if (modId.toLowerCase().indexOf(searchFilter.toLowerCase()) != -1) {
                    filteredDisabledCount++;
                }
            }

            modsTxt.text = "Showing " + getFilteredModCount(searchFilter) + " of " + totalMods + " mods";
            if (filteredDisabledCount > 0) disModsTxt.text = "(+" + filteredDisabledCount + " disabled)";
        }
    }

    function getModIcon(mod:ModMetadata, disabled:Bool = false):BitmapData {
        if (!disabled && mod.icon != null)
            return BitmapData.fromBytes(ByteArrayData.fromBytes(mod.icon));

        if (disabled && FileUtilSandboxed.fileExists("mods/" + mod.title + "/_polymod_icon.png"))
            return BitmapData.fromFile("mods/" + mod.title + "/_polymod_icon.png");

        return Assets.getBitmapData(FlxG.random.bool(5) ? "flixel/images/logo/default.png" : Paths.image("modmenu/default_icon"));
    }

    // Filter Functions
    function applySearchFilter(list:ListView, searchFilter:String) {
        var filter = searchFilter.toLowerCase();
        list.dataSource.filter((index:Int, item:Dynamic) -> {
            var modId:String = item.modId;
            return modId != null && modId.toLowerCase().indexOf(filter) != -1;
        });
    }

    function getFilteredModCount(searchFilter:String):Int {
        if (searchFilter == "") return totalMods;

        // Let the dataSource handle the filtering and just get its size
        return modsListView.dataSource.size;
    }

    // Other
    override function onOpenSubStateComplete(targetState:FlxSubState):Void {
        super.onOpenSubStateComplete(targetState);
        trace("Open SubState");
        modMenuCam.visible = false;
    }

    override function onCloseSubStateComplete(targetState:FlxSubState):Void {
        super.onCloseSubStateComplete(targetState);
        trace("Close SubState");
        FlxG.state.camera.filters = [new ShaderFilter(new GaussianBlurShader(1))];
        modMenuCam.visible = true;
    }

    // Cleanup stuff
    override function close() {
        refreshModStates();
        cleanup();
        super.close();
    }

    override function destroy() {
        cleanup();
        super.destroy();
    }

    function cleanup() {
        if (modMenuCam != null) FlxG.cameras.remove(modMenuCam);
        if (FlxG.state.camera.filters != null) FlxG.state.camera.filters.remove(FlxG.state.camera.filters[FlxG.state.camera.filters.length - 1]);
        FlxG.state.persistentUpdate = prevPersUpd;
        if (prevCursor == false) Cursor.hide();
    }

    function refreshModStates() {
        if (!modStates.keys().hasNext()) return;

        for (mod in modStates.keyValueIterator()) {
            var currentFile = mod.value ? "_polymod_meta_disabled.json" : "_polymod_meta.json";
            var targetFile = mod.value ? "_polymod_meta.json" : "_polymod_meta_disabled.json";
            var modPath = "mods/" + mod.key + "/" + currentFile;
            FileUtilSandboxed.rename(modPath, targetFile);
        }

        PolymodHandler.forceReloadAssets();
    }
}